apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.garfield.study"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]   //设置给编译处理器的参数
            }
        }
    }
    signingConfigs {
        release {
            storeFile file("../MyAndroid.jks")
            storePassword "android"
            keyAlias "weishu"
            keyPassword "android"
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        baidu {
            println("baidu productFlavors")
            applicationId "com.garfield.study.baidu"   //可以同时在手机上安装
            resValue "string", "app_name", "Study"
        }
        xiaomi {
            println("xiaomi productFlavors")
            applicationIdSuffix "xiaomi"
            resValue "string", "app_name", "Study_x"
        }
    }
    sourceSets {
        main {}
        xiaomi {
            assets.srcDirs = ['src/xiaomi/assets']   //默认就是
        }
        baidu {}
    }
    greendao {
        targetGenDir 'src/main/java'
        daoPackage 'com.garfield.study.greendao.gen'
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith(".apk")) {
                def fileName;
                if (variant.buildType.name == "release") {
                    fileName = "study_${variant.productFlavors[0].name}_release"+ ".apk"
                } else {
                    fileName = "study_${variant.productFlavors[0].name}_debug" + ".apk"
                }
                output.outputFile = new File(outputFile.parent, fileName);
            }
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.nineoldandroids:library:2.4.0'
    compile project(':baselib')

    compile 'org.greenrobot:greendao:3.2.2'
    compile project(':apt-annotation')
    annotationProcessor project(':apt-compiler')
}
